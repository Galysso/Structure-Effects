architectury {
    common project.findProperty("enabled_platforms").toString().split(',')
}

repositories {
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" } // <- Sponge
}

// Enabling / disabling integration with other mods
def wraith_waystones_integration = rootProject.ext.VERS.containsKey('wraith_waystones_version')

// Dependencies
dependencies {
    //compileOnly "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    compileOnly "org.ow2.asm:asm:${project.asm_version}"

    modCompileOnly "me.fzzyhmstrs:fzzy_config:${rootProject.ext.VERS.fabric_fzzy_config_version}"

    compileOnly "org.spongepowered:mixin:${project.mixin_version}"
    annotationProcessor "org.spongepowered:mixin:${project.mixin_version}:processor"
}

// Categories of version-specific sources
def cats = ["nbt", "registry", "saved_data", "server_player"]

// Adds the found directory to the source sets, if any
sourceSets {
    main {
        java {
            srcDir "src/main/java"
            println "Searching compatilibity sources for MC ${project.minecraft_version}..."

            // Minecraft version specific sources
            cats.each { cat ->
                def best = pickBest(project, "src/compat/${cat}")
                if (best && new File(best, "java").exists()) {
                    println ">> Compat ${cat}: using ${best}/java"
                    srcDir "${best}/java"
                }
            }
        }
    }
}